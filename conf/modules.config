/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/


def new_publish_dir(new_attributes=[:]) { 
    def default_publish_dir = [
        enabled: params.publish_all,
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    return default_publish_dir + new_attributes
}

process {

    publishDir = new_publish_dir()

    withName: BCFTOOLS_VIEW {
        container = 'staphb/bcftools:1.20'
        ext.args = { '-Oz --write-index=tbi' }
        ext.prefix = {meta.id + ".standardized.g"}
    }

    withName: BCFTOOLS_FILTER {
        container = 'staphb/bcftools:1.20'
        ext.args = {'-e \'strlen(REF)>1 & strlen(REF)==strlen(ALT) & TYPE="snp"\' -Oz --write-index=tbi'}
        ext.prefix = {meta.id + ".filtered"}
    }

    withName: BCFTOOLS_NORM {
        container = 'staphb/bcftools:1.20'
        ext.args = {'-c w -d all -Oz --write-index=tbi'}
        ext.prefix = {meta.id + ".normalized"}
    }

    withName: COMBINEGVCFS {
        container = 'broadinstitute/gatk:4.5.0.0'
    }

    withName: GATK4_GENOTYPEGVCFS {
        container = 'broadinstitute/gatk:4.5.0.0'
        ext.args = params.allow_old_gatk_data ? "--allow-old-rms-mapping-quality-annotation-data" : ""
        ext.prefix = {meta.id + ".genotyped"}
    }

    withName: GATK4_VARIANTFILTRATION {
        container = 'broadinstitute/gatk:4.5.0.0'
        ext.prefix = {meta.id + ".hardfiltered"}
        //collects all filters given as parameter, separating by name and expression
        def filters = params.hardFilters
        ext.args = {filters.collect{ "--filter \"${it.expression}\" --filter-name \"${it.name}\"" }.join(" ")}
    }

    withName: EXOMISER { 
        container = 'ferlabcrsj/exomiser:2.4.1'
        publishDir = new_publish_dir([
            enabled: true,
            pattern: 'results/*{vcf.gz,vcf.gz.tbi,tsv,json,html}'
        ])
        ext.args = { "--output-filename=${meta.id}.exomiser" }
    }

    withName: ENSEMBLVEP_VEP {
        container = 'ensemblorg/ensembl-vep:release_111.0' //sticking to v111 for now, but we should update this
        def args_list =  [
            "--offline",
            "--format vcf",
            "--vcf",
            "--xref_refseq",
            "--variant_class",
            "--numbers",
            "--hgvs",
            "--hgvsg",
            "--canonical",
            "--symbol",
            "--flag_pick",
            "--no_stats",
            "--fields \"Allele,Consequence,IMPACT,SYMBOL,Feature_type,Gene,PICK,Feature,EXON,BIOTYPE,INTRON,HGVSc,HGVSp,STRAND,CDS_position,cDNA_position,Protein_position,Amino_acids,Codons,VARIANT_CLASS,HGVSg,CANONICAL,RefSeq\""
        ]
        ext.args = args_list.join(" ")
        ext.prefix =  {"variants.${meta.id}.vep"}
    }

    withName: '.*VCF_ANNOTATE_ENSEMBLVEP.*' {
        publishDir = new_publish_dir([enabled: true, path: { "${params.outdir}/ensemblvep" }])
    }

    withName: TABIX_TABIX {
        container = 'staphb/htslib:1.20'
    }

    withName: 'splitMultiAllelics' {
        container = 'staphb/bcftools:1.20'
        publishDir = new_publish_dir([enabled: true])
    }
}
